# Behaviors
include::common.adoc[]
:description: Behaviors allow to define logic for a parent transformation (like a game object -- player, enemy, vehicle...) and attach / detach this logic at run-time and design-time.

== Introduction

_Behaviors_ allow to customize the parent cgeref:TCastleTransform[] behavior.

A _behavior_ is a class descending from cgeref:TCastleBehavior[] that you add to parent using cgeref:TCastleTransform.AddBehavior[].

[source,pascal]
----
MyBehavior := TMyBehavior.Create(...);
MyTransform.AddBehavior(MyBehavior);
----

Using behaviors allows to:

. Encapsulate (separate) the logic of some action as a behavior class,
. The behavior can be freely added and removed at runtime.

First example behavior is in the _"New Project -> 3D FPS Game"_ template. See how the simple enemy AI is implemented there, in https://github.com/castle-engine/castle-engine/blob/master/tools/castle-editor/data/project_templates/3d_fps_game/files/code/gameenemy.pas[GameEnemy unit].

You can query for behavior existence. In the simplest case just use cgeref:TCastleTransform.FindBehavior[] method to check whether a given cgeref:TCastleTransform[] contains a behavior. To iterate over all behaviors in a given tree use cgeref:TCastleTransform.FindAllBehaviors[].

[NOTE]
====
In gamedev terminology, our _"behaviors"_ are somewhat similar to what Unity3D or https://en.wikipedia.org/wiki/Entity_component_system[Entity Component Systems] call _"components"_. That is, you can add and remove _behaviors_ at runtime, and various game objects (like player, enemy, vehicle...) can be composed from multiple behaviors, and behaviors define both data and functionality.

We use the term _"behavior"_ throughout our engine, not _"component"_, because in the Pascal context the word _"component"_ is already associated with the `TComponent` class. And `TComponent` is an important standard Pascal class that is a basis (used also by our engine) for everything that can be visually edited and serialized. It's something _completely_ independent from the concept of _"behavior"_ that we introduce here.
====

[NOTE]
====
There are alternative ways to operate on cgeref:TCastleTransform[]:

* You can create cgeref:TCastleTransform[] descendants and override there methods like cgeref:TCastleTransform.Update[].
//But this isn't so flexible: you cannot then attach / detach the behavior at run-time or design-time freely, and you need to choose a sensible inheritance path. This approach doesn't scale if your object has multiple behaviors.

* You can operate on cgeref:TCastleTransform[] instances from the view (e.g. from `TViewMain.Update`). See link:viewport_and_scenes_from_code[Writing code to modify scenes and transformations].

But behaviors remain our recommended approach, because it nicely _scales_ when you need multiple behaviors.
====

== Built-in behaviors

The engine comes with a number of ready behavior classes available:

- cgeref:TCastleSoundSource[] (see link:sound[sound]),

- cgeref:TCastleBillboard[],

- A few behaviors related to link:physics[]:
+
--
- cgeref:TCastleRigidBody[],
- colliders (derived from cgeref:TCastleCollider[]),
- physics joints.
--

- cgeref:TCastleStickToSurface[] (useful with our cgeref:TCastleTerrain[]).

- _Experimental_: cgeref:TCastleLiving[], cgeref:TCastleMoveAttack[] and friends. See the cgeref:CastleLivingBehaviors[] unit and demos of it in https://github.com/castle-engine/castle-engine/tree/master/examples/creature_behaviors[examples/creature_behaviors] and https://github.com/castle-engine/conference-zlot-delphi-2024[conference-zlot-delphi-2024].

== Examples of implementing your own behaviors

=== Example: keep moving forward

Let's modify our cars demo from link:viewport_and_scenes_from_code[Writing code to modify scenes and transformations] example to use behaviors to move each car.

. Define and implement class `TCarBehavior` that descends from cgeref:TCastleBehavior[], and moves a car. Place this code in your project (you can create a new unit, or add it to the implementation of `GameViewMain` unit):
+
[source,pascal]
----
type
  TCarBehavior = class(TCastleBehavior)
  public
    procedure Update(const SecondsPassed: Single; var RemoveMe: TRemoveType); override;
  end;

procedure TCarBehavior.Update(const SecondsPassed: Single; var RemoveMe: TRemoveType);
var
  T: TVector3;
begin
  inherited;

  T := Parent.Translation;
  { Thanks to multiplying by SecondsPassed, it is a time-based operation,
    and will always move 40 units / per second along the +Z axis. }
  T := T + Vector3(0, 0, 40) * SecondsPassed;
  { Wrap the Z position, to move in a loop }
  if T.Z > 70 then
    T.Z := -50;
  Parent.Translation := T;
end;
----

. Create an instance of `TCarBehavior` (owned by `FreeAtStop`) and insert it into each `CarTransforms[I]` instance. To do this, extend the loop in `TViewMain.Start` that creates `CarTransforms[I]` instances from previous section. After this line:
+
[source,pascal]
----
CarTransforms[I].Add(CarScene);
----
add a new line:
+
[source,pascal]
----
CarTransforms[I].AddBehavior(TCarBehavior.Create(FreeAtStop));
----
+
NOTE: The order doesn't really matter much, and you can rearrange most of the code we describe. In general, you can add children and behaviors in any order.

. Remove previous movement logic from `TViewMain.Update`, if you added it by following the link:viewport_and_scenes_from_code[Writing code to modify scenes and transformations] chapter. We don't need it anymore, as new behavior class will move the car. So make `TViewMain.Update` as simple as it was at the beginning:
+
[source,pascal]
----
procedure TViewMain.Update(const SecondsPassed: Single; var HandleInput: Boolean);
begin
  inherited;
  { This virtual method is executed every frame.}
  LabelFps.Caption := 'FPS: ' + Container.Fps.ToString;
end;
----

Test and run. Play around with adding behaviors to only some of the cars (i.e. only cars with `I < 10`) to feel the flexibility of this.

=== Example: controlling the footsteps sound

A behavior can be used to connect some other components with a custom functionality. For example this simple `TFootstepsBehavior` can be attached to the camera, and

- It assumes that the parent defines another behavior, cgeref:TCastleSoundSource[], that can play footsteps sound,

- It requires you to set `Navigation` property to a cgeref:TCastleWalkNavigation[] instance, so that it can observe cgeref:TCastleWalkNavigation.IsWalkingOnTheGround[] to know when to make a footsteps sound.

The sample implementation is as follows:

[source,pascal]
----
type
  { Play footsteps sound, when we're walking.
    This simply observes Navigation.IsWalkingOnTheGround,
    and controls the playback of FootstepsSoundSource
    (which is TCastleSoundSource found on parent). }
  TFootstepsBehavior = class(TCastleBehavior)
  strict private
    FootstepsSoundSource: TCastleSoundSource;
  public
    { Navigation to observe.
      Set it right after creation, cannot remain @nil for Update. }
    Navigation: TCastleWalkNavigation;
    procedure ParentAfterAttach; override;
    procedure Update(const SecondsPassed: Single; var RemoveMe: TRemoveType); override;
  end;

procedure TFootstepsBehavior.ParentAfterAttach;
begin
  inherited;
  FootstepsSoundSource := Parent.FindBehavior(TCastleSoundSource) as TCastleSoundSource;
end;

procedure TFootstepsBehavior.Update(const SecondsPassed: Single; var RemoveMe: TRemoveType);
begin
  inherited;
  FootstepsSoundSource.SoundPlaying := Navigation.IsWalkingOnTheGround;
end;
----

You can initialize it like this from code:

[source,pascal]
----
procedure TViewPlay.Start;
var
  Footsteps: TFootstepsBehavior;
begin
  inherited;
  Footsteps := TFootstepsBehavior.Create(FreeAtStop);
  Footsteps.Navigation := WalkNavigation;
  PlayerCamera.AddBehavior(Footsteps);
end;
----

This simple implementation works, but you will notice an important problem: the footsteps sound may stop and start too often, if you walk on an uneven terrain. You can take a look at the https://github.com/castle-engine/lynch/blob/master/code/gamebehaviors.pas[example of TFootstepsBehavior from the "Lynch" demo] that avoids this problem, by not stopping the footsteps playback immediately. Thanks to the power of behaviors, this complicated mechanism is nicely encapsulated inside a `TFootstepsBehavior` class.

=== Example: observer that keeps looking at / pointing at target

Sometimes you want one object (like a spotlight, or a camera, or some auto-aiming weapon) to "look at" (or "point at") another object.

To do this you can set the observer's cgeref:TCastleTransform.Direction[] every frame. Like this:

[source,pascal]
----
type
  TLookAtBehavior = class(TCastleBehavior)
  public
    Target: TCastleTransform;
    procedure Update(const SecondsPassed: Single; var RemoveMe: TRemoveType); override;
  end;

procedure TLookAtBehavior.Update(const SecondsPassed: Single; var RemoveMe: TRemoveType);
begin
  inherited;
  // Note: "Parent" here refers to the observer, the object that should look at Target.
  Parent.WorldDirection := Target.WorldTranslation - Parent.WorldTranslation;
end;
----

Add a new instance of this class to the cgeref:TCastleTransform[] that should act as an _"observer"_ . Like this:

[source,pascal]
----
procedure TMyView.Start;
var
  LookAtBehavior: TLookAtBehavior;
begin
  inherited;
  LookAtBehavior := TLookAtBehavior.Create(FreeAtStop);
  LookAtBehavior.Target := SomeTarget;
  Observer.AddBehavior(LookAtBehavior);
end;
----

Notes:

- The _observer_ above can be any instance of cgeref:TCastleTransform[], including a camera or light source (since cgeref:TCastleCamera[] and lights like cgeref:TCastleSpotLight[] are also descendants of cgeref:TCastleTransform[]).

- Example above calculates and sets things in the _"world coordinate space"_. This should work with any setup, even if `Target` and `Observer` are in very different places in the transformation hierarchy.
+
If they are in same same coordinate space, e.g. under the same parent, then you could even simplify it, dropping the `World` prefixes, just doing `Parent.Direction := Target.Translation - Parent.Translation;`.

- Note that setting the cgeref:TCastleTransform.Direction[] is really just a shortcut for setting the cgeref:TCastleTransform.Rotation[] property. The `Direction` and `Up` vectors together determine the `Rotation`. (And in turn, setting cgeref:TCastleTransform.WorldDirection[] is just a shortcut for setting cgeref:TCastleTransform.Direction[] but converting the vector to the proper coordinate space first.)
+
There are other ways to set the rotation, like using cgeref:TCastleTransform.Rotation[] or cgeref:TCastleTransform.SetView[] or cgeref:TCastleTransform.SetWorldView[]. In different cases, different methods may be more convenient / useful.

- The `Target.WorldTranslation - Parent.WorldTranslation` calculates a vector whose length doesn't matter. We only care about the direction of it. When we assign it to `Parent.WorldDirection`, the property setter will ignore the vector's length, internally "normalizing" the provided vector. You don't need to know / care about this, in short.

== Registering behaviors to use at design-time (in CGE editor)

As any other component, behaviors can be registered as link:custom_components[custom component] for the CGE editor. This way you can add and configure behaviors at design-time.

Simply call cgeref:RegisterSerializableComponent[] to register the behavior, like

[source,pascal]
----
unit MyBehavior;

interface

uses CastleTransform;

type
  TMyBehavior = class(TCastleBehavior)
  end;

implementation

uses CastleComponentSerialize;

initialization
  RegisterSerializableComponent(TMyBehavior, 'My Behavior');
end.
----

Inside your link:project_manifest[CastleEngineManifest.xml] file, set the attribute `editor_units` to list all the units that call the `RegisterSerializableComponent`, like `editor_units="MyBehavior"`.
